INS
         TTL "APP-L-CACHE FAST WRITE UTILITY"
         ORG $4000
         OBJ $0800
;---------------------------------
;
; EQUATES
;
;---------------------------------
;
; IOB OFFSETS
;
DRIVE    EPZ 2
TRACK    EPZ 4
SECTOR   EPZ 5
BUFFERLO EPZ 8
BUFFERHI EPZ 9
COMMAND  EPZ 12
ERROR    EPZ 13
;
; IOB COMMANDS
;
READ     EPZ 1
WRITE    EPZ 2
;
; ZERO PAGE LOCATIONS
;
;    (NOT USED BY DOS; CAUTION
;       WITH APPLICATIONS )
;
CURSECT  EPZ 6
SINDEX   EPZ 7
TINDEX   EPZ 8
;
; CONSTANTS
;
MAXSECT  EPZ $F
DOT      EPZ $AE             ;"."
;
; ABSOLUTE LOCATIONS
;
RWTS     EQU $BD00
SZFLG    EQU $BCFE           ;SIZE OF APP-L-CACHE
CACHEOFF EQU $C082           ;SELECT APPLESOFT ROMS
CACHEON  EQU $C08B           ;SELECT APP-L-CACHE RAM
BANK     EQU $C08F
ROMSW    EQU $C08F           ;TURN CACHE ROM ON
RESTBANK EQU $C809           ;RESTORE CURRENT BANK @ $1E & $1F
SAVEBANK EQU $C80C           ;SAVE CURRENT BANK @ $1E & $1F
ROMSOFF  EQU $CFFF           ;TURN OFF ALL PERIPHERAL CARD ROMS
BUFFER   EQU $D000           ;START OF APP-L-CACHE BANK
KEYIN    EQU $FD1B           ;READ A KEY
CROUT    EQU $FD8E           ;PRINTS <CR>
PRBYTE   EQU $FDDA           ;PRINTS HEX BYTE
COUT     EQU $FDED
;
; SKIP OVER TABLES
;
START    JMP CODESTRT
;---------------------------------
;
; IOB TABLE
;
;---------------------------------
IOB      BYT 1
         BYT $60
         BYT 1
         DFS 3,0
         ADR DEVTAB
         ADR 0
         ADR 0
         BYT 1
         ADR 0
         BYT $60
         BYT 1
DEVTAB   BYT 0
         BYT 1
         DBY $EFD8
;---------------------------------
;
; SECTOR OFFSET TABLE
;
;---------------------------------
SOFFSET  BYT $F
         BYT $E
         BYT $D
         BYT $C
         BYT $B
         BYT $A
         BYT $9
         BYT $8
         BYT $7
         BYT $6
         BYT $5
         BYT $4
         BYT $3
         BYT $2
         BYT $1
         BYT $0
CODESTRT:
;---------------------------------
;  
; ROUTINE: FSTWRITE  
; 
; PURPOSE: BACKUP APP-L-CACHE TO 
;   A MINI-DRIVE  
;   
; ENTRY: IOB DRIVE = DRIVE
; 
; EXIT: NONE  
;    
;---------------------------------
FSTWRITE LDA #WRITE
         LDX #COMMAND
         STA IOB,X
         JSR DODISK
         RTS 
;---------------------------------
;
; ROUTINE: DODISK 
;
; PURPOSE: READ OR WRITE MINI-DISK
;   FROM OR TO APP-L-CACHE
;
; ENTRY: IOB COMMAND = READ/WRITE
;
; EXIT: NONE
;
;---------------------------------
DODISK   JSR SAVECARD
         LDA #$22
         STA TINDEX
TRLOOP   BIT CACHEOFF        ;ENABLE MONITOR ROM
         LDA #DOT
         JSR COUT
         BIT CACHEON         ;ENABLE CACHE RAM
         BIT CACHEON
         LDA TINDEX
         LDX #TRACK
         STA IOB,X
         SEC 
         LDA #$3B
         SBC TINDEX
         STA BANK            ;BANK = $3B - TRACK
         LDA #BUFFER
         LDX #BUFFERLO
         STA IOB,X
         LDA /BUFFER
         LDX #BUFFERHI
         STA IOB,X
         JSR DOTRACK
         DEC TINDEX
         BPL TRLOOP
         BIT CACHEOFF
         JSR RESTCARD
         JSR CROUT
         RTS 
;---------------------------------
;
; ROUTINE: DOTRACK
;
; PURPOSE: READ OR WRITE A MINI 
;   TRACK FROM OR TO A 4K BUFFER
;
; ENTRY: IOB TRACK = TRACK
;        IOB BUFFER = STRT OF 4K 
;          BUFFER
;
; EXIT: NONE
;
;---------------------------------
DOTRACK  LDA #$0
         STA CURSECT
         LDA #MAXSECT
         STA SINDEX
DOSECTOR LDY CURSECT
         LDA SOFFSET,Y
         LDX #SECTOR
         STA IOB,X
         CLC 
         ADC /BUFFER
         LDX #BUFFERHI
         STA IOB,X
         LDA /IOB
         LDY #IOB
         JSR RWTS
         INC CURSECT
         DEC SINDEX
         BPL DOSECTOR
         RTS 
;---------------------------------
;
; ROUTINE: SAVECARD
;
; PURPOSE: SAVE STATUS OF APP-L-
;   CACHE
;
;---------------------------------
SAVECARD BIT ROMSOFF
         BIT ROMSW
         JSR SAVEBANK
         RTS 
;---------------------------------
;
; ROUTINE: RESTCARD
;
; PURPOSE: RESTORE STATUS OF APP-L
;   -CACHE
;
;---------------------------------
RESTCARD BIT ROMSOFF
         BIT ROMSW
         JSR RESTBANK
         RTS 
         END 

